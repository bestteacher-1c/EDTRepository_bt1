&НаКлиенте
Перем КартинкаИзменилась;

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ДвоичныеДанныеКартинки = ТекущийОбъект.Значение.Получить();

	Если ДвоичныеДанныеКартинки <> Неопределено Тогда

		ОбластьКартинки = ПоместитьВоВременноеХранилище(ДвоичныеДанныеКартинки, УникальныйИдентификатор);

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Заголовок = Строка(Запись.Номенклатура);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	КартинкаИзменилась = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если КартинкаИзменилась = Истина Тогда

		ПараметрыЗаписи.Вставить("КартинкаИзменилась");

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ПараметрыЗаписи.Свойство("КартинкаИзменилась") = Истина Тогда

		ДвоичныеДанныеКартинки = ПолучитьИзВременногоХранилища(ОбластьКартинки);

		ТекущийОбъект.Значение = Новый ХранилищеЗначения(ДвоичныеДанныеКартинки);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбластьКартинкиНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Обработчик = Новый ОписаниеОповещения("ЗагрузитьКартинку", ЭтотОбъект);

	НачатьПомещениеФайла(Обработчик, , , Истина, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКартинку(ФайлВыбран, АдресВПамяти, ИмяФайла,
		ДопПараметры) Экспорт

	Если (ФайлВыбран = Истина) Тогда

		Если (РазмерКартинкиКорректный(АдресВПамяти) = Истина) Тогда
			ОбластьКартинки = АдресВПамяти;

			Модифицированность = Истина;

			КартинкаИзменилась = Истина;

		Иначе

			ПоказатьПредупреждение(, "Неверный размер картинки!");

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция РазмерКартинкиКорректный(АдресКартинки)

	ДанныеКартинки = ПолучитьИзВременногоХранилища(АдресКартинки);

	Картинка = Новый Картинка(ДанныеКартинки);

	Картинка = Картинка.Преобразовать(ФорматКартинки.PNG);

	ЗаголовокФайла = "" + Картинка.ПолучитьДвоичныеДанные();

	СегментРазмера = Сред(ЗаголовокФайла, 49, 23);

	Возврат СегментРазмера = "00 00 00 40 00 00 00 40";

КонецФункции








